// reproduction of "Spatial stochastic model[...] of the Hes1 gene regulatory network[...]"
// by Sturrock et al. 2012, J R Soc Interface

 k1 = 5/3 // 1/(M*min); binding rate of Hes1 to promoter
 k2 = 0.1 // 1/min; unbinding of Hes1 and promoter
 alphaM = 3 // 1/min; transcription rate
 gamma = 30 // dimensionless factor for transcription repression
 alphaP = 1 // 1/min; translation rate
 muM = 0.015 // 1/min; degradation of hes1 mRNA
 muP = 0.043 // 1/min; degradation of Hes1 protein
 D = 0.6 // micro-m²/min; molecular diffusion
 nucDiffFactor = 0
 
 sizePromProtMultiplier = 1;
 rCell = 7.5;
 rNucleus = 3;
 rPromoter = 0.1*sizePromProtMultiplier;
 
 protSize = 0.01*sizePromProtMultiplier;
 mRNASize = protSize/2;
 
  mRNASize = protSize/3;
 
 rProt = (protSize*3/4/pi)^1/3;
 k1RateOrProb = if protSize==0 then k1 else k1 / (4*pi*(rProt+rPromoter)*D);
 
 promSide = 1;
 
 Cell(shape:ball,size:4/3*pi*rCell^3,position:(0,0,0));
 Nucleus(shape:ball,size:4/3*pi*rNucleus^3,position:(0,0,0),diffusion:D*nucDiffFactor);
 Promoter(shape:ball,size:4/3*pi*rPromoter^3,position:((rNucleus-rPromoter-1e-10)*promSide,0,0),site:{"free","occ"});
 Protein(shape:ball,size:protSize,diffusion:D);
 mRNA(shape:ball,size:mRNASize,diffusion:D);
 
 1 Cell[1 Nucleus[1 Promoter + 10 mRNA] + 60 Protein]; 
 
 prot_in: Nucleus + Protein -> Nucleus[Protein] @ 1
 prot_out: Nucleus[Protein] -> Nucleus + Protein @ 1
 rna_in: Nucleus + mRNA -> Nucleus[mRNA] @ 1
 rna_out: Nucleus[mRNA] -> Nucleus + mRNA @ 1
 
 prom_bind:   Promoter(site=="free") + Protein -> Promoter(site="occ") @ k1RateOrProb
 prom_unbind: Promoter(site=="occ") -> Promoter(site="free") + Protein @ k2
 
 transc_normal: Promoter(site=="free") -> Promoter +  mRNA @ alphaM
 tranc_repr: Promoter(site=="occ") -> Promoter + mRNA @ alphaM/gamma
 translation: Cell[mRNA] -> Cell[mRNA + Protein] @ alphaP
 degr_rna: mRNA ->  @ muM
 degr_prot: Protein -> @ muP